# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor : Xpander <xpander0@gmail.com>

_ver=1.14
_pkgbase=mate-screensaver
pkgname=(${_pkgbase})
pkgver=${_ver}.0
pkgrel=2
pkgdesc='Screensaver for MATE (GTK2 version)'
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'libmatekbd>=1.14' 'libnotify' 'libxss' 'mate-desktop>=1.14'
         'mate-menus>=1.14' 'mate-session-manager>=1.14')
makedepends=('mate-common' 'xmlto')
optdepends=('mate-power-manager: Add power management support.'
            'mate-screensaver-hacks: Additional screensavers.'
            'rss-glx: Really slick screensavers.')
groups=('mate-extra')
conflicts=("${_pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('SKIP')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --with-xscreensaverdir=/usr/share/xscreensaver/config \
        --with-xscreensaverhackdir=/usr/lib/xscreensaver \
        --with-mit-ext \
        --with-libnotify \
        --enable-locking \
        --with-gtk=2.0 \
        --without-console-kit \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
